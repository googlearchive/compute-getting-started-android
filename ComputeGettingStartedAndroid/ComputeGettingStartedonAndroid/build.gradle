buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 17
    }
}

dependencies {
    compile 'com.android.support:support-v4:13.0.+'

    // BEGIN Google APIs

    // The following lines implement maven imports as defined at:
    // https://code.google.com/p/google-api-java-client/wiki/Setup

    // Add the Google API client library.
    compile(group: 'com.google.api-client', name: 'google-api-client', version: '1.16.0-rc') {
        // Exclude artifacts that the Android SDK/Runtime provides.
        exclude(group: 'xpp3', module: 'xpp3')
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
        exclude(group: 'junit', module: 'junit')
        exclude(group: 'com.google.android', module: 'android')
    }

    // Add the Android extensions for the Google API client library.
    compile(group: 'com.google.api-client', name: 'google-api-client-android',
            version: '1.16.0-rc') {
        // Exclude play services. Explicitly include it above after you have installed Play
        // services from the Android SDK manager.
        exclude(group: 'com.google.android.google-play-services', module: 'google-play-services')
    }

    // Add Play Services which is used to authenticate the application to Google.
    // This line requires that Google Play Services was installed using the Android SDK manager. If
    // you experience difficulty importing the project you can comment this line, import, then
    // download Play Services afterward.
    compile(group: 'com.google.android.gms', name: 'play-services', version: '3.1.36')

    // END Google APIs

    // The following client libraries make HTTP/JSON on Android easier.

    // Android extensions for Google HTTP Client.
    compile(group: 'com.google.http-client', name: 'google-http-client-android',
            version: '1.16.0-rc') {
        exclude(group: 'com.google.android', module: 'android')
    }

    // This is used by the Google HTTP client library.
    compile(group: 'com.google.guava', name: 'guava', version: '14.0.1')

    // These are the Google Compute Engine service extensions to the Google API client library.
    compile(group: 'com.google.apis', name: 'google-api-services-compute',
            version: 'v1beta15-rev8-1.15.0-rc')
}
